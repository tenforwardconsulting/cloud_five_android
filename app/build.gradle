apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("release-signing.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

if (file('my.gradle').exists()) {
    apply from: 'my.gradle'
}

apply from: 'functions.gradle'

android {
    compileSdkVersion 27

    if (keystorePropertiesFile.exists()) {
        signingConfigs {
            release {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    defaultConfig {
        applicationId "com.cloudfiveapp.android"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'GIT_HASH', "\"${gitHash()}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions "default"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // region Test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    // endregion

    // region Support Libs
    ext.supportLibraryVersion = '27.0.2'
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta5'
    // endregion

    // region Arch Components
    ext.architectureComponentsVersion = '1.1.0'
    implementation "android.arch.lifecycle:extensions:$architectureComponentsVersion"
    // endregion

    // region Material UI
    implementation "com.afollestad.material-dialogs:core:0.9.6.0"
    // endregion

    // region DI
    ext.daggerVersion = '2.12'
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    // endregion

    // region RxJava
    ext.rxJavaVersion = '2.1.0'
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxJavaVersion"

    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"

    ext.rxBindingVersion = '2.0.0'
    implementation "com.jakewharton.rxbinding2:rxbinding:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxBindingVersion"
    // endregion

    // region Net
    ext.retrofitVersion = '2.3.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit-mock:$retrofitVersion"

    implementation "com.squareup.okhttp3:logging-interceptor:3.9.1"

    implementation "com.squareup.moshi:moshi:1.5.0"
    implementation "com.squareup.moshi:moshi-kotlin:1.5.0"
    // endregion

    // region Debug
    implementation "com.jakewharton.timber:timber:4.6.1"
    implementation "com.facebook.stetho:stetho-okhttp3:1.5.0"
    // endregion
}
